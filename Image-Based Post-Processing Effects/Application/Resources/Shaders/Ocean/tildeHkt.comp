#version 450 core

#define M_PI 3.1415926535897932384626433832795

layout(local_size_x = 8, local_size_y = 8, local_size_z= 1) in;

layout(rg16f, binding = 0) uniform writeonly image2D uTildeHktDx;
layout(rg16f, binding = 1) uniform writeonly image2D uTildeHktDy;
layout(rg16f, binding = 2) uniform writeonly image2D uTildeHktDz;
layout(rg16f, binding = 3) uniform readonly image2D uTildeH0k;
layout(rg16f, binding = 4) uniform readonly image2D uTildeH0minusK;


uniform int uN;
uniform int uL;
uniform float uTime;

const float g = 981.0;

struct Complex
{
	float real;
	float im;
};

Complex mul(Complex c0, Complex c1)
{
	Complex c;
	c.real = c0.real * c1.real - c0.im * c1.im;
	c.im = c0.real * c1.im + c0.im * c1.real;
	return c;
}

Complex add(Complex c0, Complex c1)
{
	Complex c;
	c.real = c0.real + c1.real;
	c.im = c0.im + c1.im;
	return c;
}

Complex conj(Complex c)
{
	Complex cConj = Complex(c.real, -c.im);
	return cConj;
}

void main() 
{
	vec2 x = vec2(gl_GlobalInvocationID.xy) - float(uN) * 0.5;
	vec2 k = vec2(2.0 * M_PI * x.x/uL, 2.0 * M_PI * x.y/uL);
	
	float mag = length(k);
	mag = max(mag, 0.00001);
	
	float w = sqrt(g * mag);
	
	vec2 tildeH0kValues = imageLoad(uTildeH0k, ivec2(gl_GlobalInvocationID.xy)).rg;
	Complex fourierCmp = Complex(tildeH0kValues.x, tildeH0kValues.y);
	
	vec2 tildeH0minusKValues = imageLoad(uTildeH0minusK, ivec2(gl_GlobalInvocationID.xy)).rg;
	Complex fourierCmpConj = conj(Complex(tildeH0minusKValues.x, tildeH0minusKValues.y));
	
	float cosWT = cos(w * uTime);
	float sinWT = sin(w * uTime);
	
	// euler formula
	Complex expIWT = Complex(cosWT, sinWT);
	Complex expIWTInv = Complex(cosWT, -sinWT);
	
	// dy
	Complex hktDy = add(mul(fourierCmp, expIWT), mul(fourierCmpConj, expIWTInv));
	
	// dx
	Complex dx = Complex(0.0, -k.x / mag);
	Complex hktDx = mul(dx, hktDy);
	
	// dz
	Complex dy = Complex(0.0, -k.y / mag);
	Complex hktDz = mul(dy, hktDy);
	
	imageStore(uTildeHktDx, ivec2(gl_GlobalInvocationID.xy), vec4(hktDx.real, hktDy.im, 0.0, 1.0));
	imageStore(uTildeHktDy, ivec2(gl_GlobalInvocationID.xy), vec4(hktDy.real, hktDy.im, 0.0, 1.0));
	imageStore(uTildeHktDz, ivec2(gl_GlobalInvocationID.xy), vec4(hktDz.real, hktDy.im, 0.0, 1.0));
}

