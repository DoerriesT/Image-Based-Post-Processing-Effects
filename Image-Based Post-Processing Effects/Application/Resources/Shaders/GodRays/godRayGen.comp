#version 450 core

layout(local_size_x = 8, local_size_y = 8, local_size_z= 1) in;

layout(binding = 0) uniform writeonly image2D uImgOut;

layout(binding = 0) uniform sampler2D uSourceTexture;

uniform vec3 uSunPos;

void main()
{	
	vec2 domainSize = textureSize(uSourceTexture, 0).xy;
	vec2 texelSize = vec2(1.0 / domainSize);
	vec2 texCoord = vec2(vec2(gl_GlobalInvocationID.xy) + 0.5) * texelSize;

	vec2 deltaTexCoord = texCoord - uSunPos.xy;

	const int SAMPLE_COUNT = 50;
	const float density = 0.9;
	const float weight = (1.0 / SAMPLE_COUNT) * 3.0;
	const float decay = pow(0.9, 100.0 / SAMPLE_COUNT);

	deltaTexCoord *= 1.0 / SAMPLE_COUNT * density;

	vec3 color = texture(uSourceTexture, texCoord).rgb;

	float illuminationDecay = 1.0;

	for (int i = 0; i < SAMPLE_COUNT; ++i)
	{
		texCoord -= deltaTexCoord;

		vec3 tap = texture(uSourceTexture, texCoord).rgb;

		tap *= illuminationDecay * weight;

		color += tap;

		illuminationDecay *= decay;
	}

	imageStore(uImgOut, ivec2(gl_GlobalInvocationID.xy), vec4(color * uSunPos.z, 1.0));
}